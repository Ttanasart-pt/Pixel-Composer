// 2024-04-28 13:06:45
enum ITERATION_STATUS {
	not_ready,
	loop,
	complete,
}

function Node_Iterator(_x, _y, _group = noone) : Node_Collection(_x, _y, _group) constructor {
	color = COLORS.node_blend_loop;
	icon  = THEME.loop;
	
	willRestart         = false;		//in the next getNextNode, reset all child nodes, use in loop.	
	managedRenderOrder  = true;
	reset_all_child     = true;
	combine_render_time = false;
	loop_start_time     = 0;
	
	iterated = 0;
	
	static isActiveDynamic = function(frame = CURRENT_FRAME) { #region
		for( var i = 0, n = ds_list_size(nodes); i < n; i++ )
			if(nodes[| i].isActiveDynamic(frame)) return true;
		
		return false;
	} #endregion
	
	static initLoop = function() { #region
		resetRender();
		
		iterated = 0;
		loop_start_time = get_timer();
		var node_list   = getNodeList();
		
		for( var i = 0; i < ds_list_size(node_list); i++ ) {
			var _node = node_list[| i];
			if(variable_struct_exists(_node, "initLoop"))
				_node.initLoop();
		}
		
		doInitLoop();
		
		LOG_LINE_IF(global.FLAG.render, "------------------< Loop begin >------------------");
	} #endregion
	
	static doInitLoop = function() {}
	
	static update = function(frame = CURRENT_FRAME) { initLoop(); }
	
	static outputNextNode = function() { #region
		LOG_BLOCK_START();	
		LOG_IF(global.FLAG.render == 1, "[outputNextNode] Get next node from Loop output");
		
		var _nodes = [];
		
		for( var i = 0; i < ds_list_size(nodes); i++ ) { // check if every node is updated
			if(!nodes[| i].rendered) {
				LOG_IF(global.FLAG.render == 1, $"Skipped due to node {nodes[| i].internalName} not rendered.");
				LOG_BLOCK_END();
				return _nodes;
			}
		}
		
		if(willRestart) {
			LOG_IF(global.FLAG.render == 1, $"Restart");
			resetRender();
			willRestart = false;
		}
		
		var _ren = iterationStatus();
		
		if(_ren == ITERATION_STATUS.loop) { //Go back to the beginning of the loop, reset render status for leaf node inside?
			LOG_IF(global.FLAG.render == 1, $"Loop restart: iteration {iterated}");
			_nodes = array_append(_nodes, __nodeLeafList(getNodeList()));
			
		} else if(_ren == ITERATION_STATUS.complete) { //Go out of loop
			LOG_IF(global.FLAG.render == 1, "Loop completed get next node external");
			setRenderStatus(true);
			_nodes = getNextNodesExternal();
		} 
		
		LOG_BLOCK_END();
		return _nodes;
	} #endregion
	
	static getIterationCount = function() { return 0; }
	
	static iterationStatus = function() { #region
		if(iterated >= getIterationCount())
			return ITERATION_STATUS.complete;
		return ITERATION_STATUS.loop;
	} #endregion
	
	static iterationUpdate = function() { #region
		var maxIter = getIterationCount();
		
		for( var i = 0; i < ds_list_size(nodes); i++ ) // check if every node is updated
			if(!nodes[| i].rendered) {
				LOG_LINE_IF(global.FLAG.render, $"------------------< Iteration update: {iterated} / {maxIter} [RENDER FAILED by {nodes[| i]}] >------------------");
				return;
			}
		
		iterated++;
		
		for( var i = 0; i < ds_list_size(nodes); i++ )
			nodes[| i].clearInputCache();
		
		if(iterated == maxIter) {
			LOG_LINE_IF(global.FLAG.render, $"------------------< Iteration update: {iterated} / {maxIter} [COMPLETE] >------------------");
			render_time = get_timer() - loop_start_time;
		} else if(iterated < maxIter) {
			LOG_LINE_IF(global.FLAG.render, $"------------------< Iteration update: {iterated} / {maxIter} [RESTART] >------------------");
			willRestart = true;
		}
	} #endregion
}